#!/usr/bin/env perl
use strict;
use warnings;

use Getopt::Long qw(:config gnu_getopt pass_through permute);

my $log_level = 'warn';
GetOptions(
  'cpanm=s'         => \my $cpanm,
  'force-mc!'       => \(my $force_mc = 1),
  'debug'           => sub { $log_level = 'debug' },
  'log|log-level=s' => \$log_level,
) or die "Error in command line arguments.\n";

if (!defined $cpanm) {
  $cpanm = `which cpanm`;
  chomp $cpanm;
}

die "can't find cpanm!\n"
  unless $cpanm && -e $cpanm;

my @libs = do {
  my @incs = `"$^X" -le"print for \@INC"`;
  chomp @incs;
  my %inc = map +($_ => 1), @incs;
  grep !$inc{$_}, 'lib', @INC;
};

my @opts;
push @opts, disable_metadb => 1
  if $force_mc;
push @opts, log_level => $log_level;

my @args = @ARGV;
unshift @args, '--metacpan'
  if $force_mc;

exec
  "$^X",
  (map "-I$_", @libs),
  "-MMetaCPAN::V0Shim::cpanm_hook".(@opts ? '='.join(',', @opts) : ''),
  $cpanm,
  @args;
